plugins {
    id 'checkstyle'
    id 'java'
    id 'antlr'
}

group 'de.unikassel.vs'
version '0.0.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    checkstyleOverride
}

task execute(type:JavaExec) {
    main = "de.unikassel.vs.asp.modelling.Main"
    classpath = sourceSets.main.runtimeClasspath
}

tasks.withType(Checkstyle) {
    checkstyleClasspath = project.configurations.checkstyleOverride
    maxWarnings = 0
}

sourceSets {
    generated {
        java.srcDir 'src-generated/antlr/main/'
    }
}

dependencies {
    checkstyleOverride group: 'com.puppycrawl.tools', name: 'checkstyle', version: '8.21'

    antlr "org.antlr:antlr4:4.7.2"

    generatedCompile group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.2'
    compile group: 'org.apache.velocity', name: 'velocity', version: '1.7'
    compile group: 'org.apache.velocity', name: 'velocity-tools', version: '2.0'

    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.2")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.2")
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'de.unikassel.vs.asp_modelling']
    outputDirectory = new File("src-generated/antlr/main/de/unikassel/vs/asp_modelling".toString())
}

test {
    useJUnitPlatform()
}

compileJava.dependsOn generateGrammarSource
compileJava.source sourceSets.generated.java, sourceSets.main.java

clean {
    delete "src-generated"
}

